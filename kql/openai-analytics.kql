ApiManagementGatewayLogs
| where OperationId == 'post-query' and IsRequestSuccess==true //completions_create'
| extend response = parse_json(ResponseBody)
| extend model = tostring(response.model)
| extend user = ApimSubscriptionId
| extend prompt_tokens = toint(response.usage.prompt_tokens)
| extend completion_tokens = toint(response.usage.completion_tokens)
| extend total_tokens = toint(response.usage.total_tokens)
| extend cost_prompt = case(
    model == "gpt-35-turbo", 0.0015, 
    model == "gpt-4", 0.03, 
    0.00)
| extend cost_completion = case(
    model == "gpt-35-turbo", 0.002, 
    model == "gpt-4", 0.06, 
    0.00)
| extend total_cost = (prompt_tokens*cost_prompt)+(completion_tokens*cost_completion)
| summarize 
    total_duration = sum(TotalTime), 
    total_prompt_tokens = sum(prompt_tokens), 
    total_completion_tokens = sum(completion_tokens), 
    total_all_tokens = sum(total_tokens),
    total_all_cost = sum(total_cost)
    by model
| render piechart 